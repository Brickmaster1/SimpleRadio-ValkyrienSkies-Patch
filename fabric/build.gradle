plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}
base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

    implementation "com.codinglitch.lexiconfig:lexiconfig-api:${lexiconfig_version}"

    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
    modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${minecraft_version}-${voicechat_version}"

    // Compatibilities

    implementation "com.codinglitch.vibrativevoice:vibrativevoice-api:${vibrative_voice_version}"

    modCompileOnly "maven.modrinth:voice-chat-interaction:fabric-${minecraft_version}-${voicechat_interaction_version}"
    modCompileOnly include("eu.pb4:polymer-reg-sync-manipulator:${polymer_version}+${minecraft_version}")
    //modRuntimeOnly "maven.modrinth:voice-chat-interaction:fabric-${minecraft_version}-${voicechat_interaction_version}"
    //modRuntimeOnly include("eu.pb4:polymer-reg-sync-manipulator:${polymer_version}+${minecraft_version}")
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("../common/src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"

            runDir "build/datagen"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

java {
    withSourcesJar()
    withJavadocJar()
}

modrinth {
    token = modrinth_token
    projectId = modrinth_id
    versionNumber = "${version}"
    versionName = "[FABRIC] ${full_mod_name} ${version}"
    uploadFile = remapJar
    additionalFiles = [sourcesJar, javadocJar]
    gameVersions = supported_versions.split(",").toList()
    loaders = ['fabric']
    changelog = file("${project.rootDir}/.changelog").text
    dependencies {
        required.project "fabric-api"
        required.project "lexiconfig"
        optional.project "voice-chat-interaction"
        //optional.project "vibrativevoice" not approve yet..
    }

    debugMode = debug_mode.toBoolean()
}

curseforge {
    apiKey = curseforge_token
    project {
        id = curseforge_id
        changelogType = "markdown"
        changelog = file("${project.rootDir}/.changelog")
        releaseType = 'release'
        relations {
            requiredDependency 'fabric-api'
            requiredDependency 'lexiconfig'
            requiredDependency 'voice-chat-interaction'
            //requiredDependency 'vibrativevoice' not approve yet..
        }

        addGameVersion "Fabric"
        for (final def ver in supported_versions.split(",") ) {
            addGameVersion ver
        }

        mainArtifact(remapJar) {
            displayName = "[FABRIC] ${full_mod_name} ${version}"
        }
        addArtifact sourcesJar
        addArtifact javadocJar
    }

    options {
        debug = debug_mode.toBoolean()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
